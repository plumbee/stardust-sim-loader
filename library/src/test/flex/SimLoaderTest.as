package
{
import com.plumbee.stardustplayer.SimLoader;
import com.plumbee.stardustplayer.emitter.EmitterValueObject;
import com.plumbee.stardustplayer.project.DisplayModes;
import com.plumbee.stardustplayer.project.ProjectValueObject;

import flash.display.BlendMode;

import flash.events.Event;

import flash.utils.ByteArray;

import idv.cjcat.stardustextended.common.clocks.ImpulseClock;
import idv.cjcat.stardustextended.common.clocks.SteadyClock;

import idv.cjcat.stardustextended.sd;

import idv.cjcat.stardustextended.twoD.emitters.Emitter2D;
import idv.cjcat.stardustextended.twoD.handlers.DisplayObjectHandler;

import org.flexunit.asserts.assertEquals;
import org.flexunit.asserts.assertFalse;
import org.flexunit.asserts.assertNotNull;
import org.flexunit.asserts.assertTrue;

import org.flexunit.async.Async;

use namespace sd;

public class SimLoaderTest
{
    [Embed(source="../resources/simWithBurstAndNormalClock.sde", mimeType="application/octet-stream")]
    private var SimWithBurstAndNormalClock:Class;
    private const simWithBurstAndNormalClock : ByteArray = new SimWithBurstAndNormalClock() as ByteArray;

    [Test(async)]
    public function projectValues_areSet() : void
    {
        var loader : SimLoader = new SimLoader();
        Async.handleEvent(this, loader, Event.COMPLETE, projectValues_areSet_loaded, 500);
        loader.loadSim( simWithBurstAndNormalClock );
    }

    private function projectValues_areSet_loaded( event : Event, passThroughData : Object) : void
    {
        const sim : ProjectValueObject = SimLoader(event.target).project;
        assertEquals( 1, sim.version );
        assertEquals( 0, sim.backgroundColor );
        assertEquals( false, sim.hasBackground );
        assertEquals( null, sim.backgroundFileName );
        assertEquals( null, sim.backgroundImage );
        assertEquals( null, sim.backgroundRawData );
        assertEquals( DisplayModes.DISPLAY_LIST, sim.displayMode );

        assertEquals( 2, sim.numberOfEmitters );
    }

    [Test(async)]
    public function emitterValues_areSet() : void
    {
        var loader : SimLoader = new SimLoader();
        Async.handleEvent(this, loader, Event.COMPLETE, emitterValues_areSet_loaded, 500);
        loader.loadSim( simWithBurstAndNormalClock );
    }

    private function emitterValues_areSet_loaded( event : Event, passThroughData : Object) : void
    {
        assertEquals( 2, SimLoader(event.target).project.numberOfEmitters );

        const emitter0 : EmitterValueObject = SimLoader(event.target).project.emitters[0];
        assertEquals( BlendMode.NORMAL, DisplayObjectHandler(emitter0.emitter.particleHandler).blendMode );
        assertEquals( 12, ImpulseClock(emitter0.emitter.clock).burstInterval );
        assertEquals( "firstEmitter", emitter0.emitter.name );
        assertNotNull( emitter0.emitter );
        assertEquals( 0, emitter0.id );
        assertNotNull( emitter0.image );
        assertEquals( "stardustEmitter_0.xml", emitter0.xmlName );
        assertEquals( "emitterImage_0.png", emitter0.imageName );

        const emitter1 : EmitterValueObject = SimLoader(event.target).project.emitters[1];
        assertEquals( BlendMode.NORMAL, DisplayObjectHandler(emitter1.emitter.particleHandler).blendMode );
	    assertTrue( emitter1.emitter.clock is SteadyClock );
        assertEquals( "emitterImage_1.png", emitter1.emitter.name );
        assertNotNull( emitter1.emitter );
        assertEquals( 1, emitter1.id );
        assertNotNull( emitter1.image );
        assertEquals( "stardustEmitter_1.xml", emitter1.xmlName );
        assertEquals( "emitterImage_1.png", emitter1.imageName );

        assertFalse( (emitter0.image == emitter1.image) );
    }

    [Test(async)]
    public function emitters_areParsedCorrectly() : void
    {
        var loader : SimLoader = new SimLoader();
        Async.handleEvent(this, loader, Event.COMPLETE, emitters_areParsedCorrectly_loaded, 500);
        loader.loadSim( simWithBurstAndNormalClock );
    }

    private function emitters_areParsedCorrectly_loaded( event : Event, passThroughData : Object) : void
    {
        const emitter0 : Emitter2D = EmitterValueObject(SimLoader(event.target).project.emitters[0] ).emitter;
        assertEquals( 3, emitter0.sd::actions.length );
        assertEquals( 5, emitter0.sd::initializers.length );
        assertEquals( 34, ImpulseClock(emitter0.clock ).impulseCount );
        assertEquals( 1, ImpulseClock(emitter0.clock ).repeatCount );
        assertTrue( (emitter0.particleHandler is DisplayObjectHandler) );
        assertEquals( BlendMode.NORMAL, DisplayObjectHandler(emitter0.particleHandler ).blendMode );

        const emitter1 : Emitter2D = EmitterValueObject(SimLoader(event.target).project.emitters[1] ).emitter;
        assertEquals( 3, emitter1.sd::actions.length );
        assertEquals( 5, emitter1.sd::initializers.length );
        assertEquals( 1, SteadyClock(emitter1.clock ).ticksPerCall );
        assertTrue( (emitter1.particleHandler is DisplayObjectHandler) );
        assertEquals( BlendMode.NORMAL, DisplayObjectHandler(emitter1.particleHandler ).blendMode );
    }

}
}
